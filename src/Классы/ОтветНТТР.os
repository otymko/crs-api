
// BSLLS:ExportVariables-off
Перем ВремяВыполнения Экспорт; // Время выполнения запроса
Перем Cookies Экспорт; // Полученные Cookies
Перем Заголовки Экспорт; // Заголовки ответа
Перем ЭтоПостоянныйРедирект Экспорт; // Ответ содержит постоянный редирект
Перем ЭтоРедирект Экспорт; // Ответ содержит редирект
Перем Кодировка Экспорт; // Кодировка ответа
Перем КодСостояния Экспорт; // Код состояния ответа
Перем URL Экспорт; // URL, по которому был запрошен ответ
// BSLLS:ExportVariables-on

Перем мДвоичныеДанныеТело;

Процедура ПриСозданииОбъекта(ДвДанные)

	Чтение = Новый ЧтениеДанных(ДвДанные);
	Буфер = Чтение.Прочитать(Мин(1024, ДвДанные.Размер())).ПолучитьБуферДвоичныхДанных();
	Позиция = 0;

	ПрочитатьСтартовуюСтроку(Буфер, Позиция);
	ПрочитатьЗаголовки(Буфер, Позиция);
	Чтение.Закрыть();

	ПрочитатьТело(ДвДанные, Позиция, Число(Заголовки.Получить("Content-Length")));

КонецПроцедуры

Процедура ПрочитатьТело(ДвДанные, Позиция, Размер)

	Чтение = Новый ЧтениеДанных(ДвДанные);
	Чтение.Пропустить(Позиция);

	мДвоичныеДанныеТело = Чтение.Прочитать(Размер).ПолучитьДвоичныеДанные();
	
КонецПроцедуры

Процедура ПрочитатьСтартовуюСтроку(Буфер, Позиция)

	СтартоваяСтрока = ПрочитатьСтрокуИзБуфера(Буфер, Позиция, "ISO-8859-1");

	Массив = СтрРазделить(СтартоваяСтрока, " ");
	КодСостояния = Число(Массив[1]);

КонецПроцедуры

Процедура ПрочитатьЗаголовки(Буфер, Позиция)

	Заголовки = Новый Соответствие;

	ТекстЗаголовок = ПрочитатьСтрокуИзБуфера(Буфер, Позиция, "ISO-8859-1");
	Пока НЕ ПустаяСтрока(ТекстЗаголовок) Цикл
		Массив = СтрРазделить(ТекстЗаголовок, ":");
		Заголовки.Вставить(СокрЛП(Массив[0]), СокрЛП(Массив[1]));

		ТекстЗаголовок = ПрочитатьСтрокуИзБуфера(Буфер, Позиция, "ISO-8859-1");
	КонецЦикла;
	
КонецПроцедуры

Функция ПрочитатьСтрокуИзБуфера(Буфер, Позиция, Кодировка = "UTF-8")
	
	Для ТекущаяПозиция = Позиция По Буфер.Размер - 1 Цикл
		лБайт = Буфер[ТекущаяПозиция];
	
		Если лБайт = 13 И ТекущаяПозиция + 1 <= Буфер.Размер - 1 И Буфер[ТекущаяПозиция + 1] = 10 Тогда
			лСтрока = ПолучитьСтрокуИзБуфераДвоичныхДанных(Буфер.Прочитать(Позиция, ТекущаяПозиция - Позиция), Кодировка);
			Позиция = ТекущаяПозиция + 2;
			Возврат лСтрока;
		КонецЕсли;
	КонецЦикла;

	лСтрока = ПолучитьСтрокуИзБуфераДвоичныхДанных(Буфер.Прочитать(Позиция, Буфер.Размер - 1 - Позиция), Кодировка);
	Позиция = Буфер.Размер;
	Возврат лСтрока;
	
КонецФункции

Функция Json() Экспорт

	Возврат "";
	
КонецФункции

Функция Текст(КодировкаТекста = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(КодировкаТекста) Тогда
		Если ЗначениеЗаполнено(Кодировка) Тогда
			КодировкаТекста = Кодировка;
		Иначе
			КодировкаТекста = "utf-8";
		КонецЕсли;
	КонецЕсли;
	
	ДвДанные = мДвоичныеДанныеТело;

	Если ДвДанные.Размер() >= 3 Тогда
		Чтение = Новый ЧтениеДанных(ДвДанные);
		Буфер = Чтение.Прочитать(3).ПолучитьБуферДвоичныхДанных();
		лЕстьМаркерВОМ = (ПолучитьHexСтрокуИзБуфераДвоичныхДанных(Буфер) = "EFBBBF");
		Чтение.Закрыть();

		Если лЕстьМаркерВОМ Тогда
			Чтение = Новый ЧтениеДанных(ДвДанные);
			Чтение.Пропустить(3);
			ДвДанные = Чтение.Прочитать().ПолучитьДвоичныеДанные();
			Чтение.Закрыть();
		КонецЕсли;
	КонецЕсли;

	Результат = ПолучитьСтрокуИзДвоичныхДанных(ДвДанные, КодировкаТекста);

	Если Результат = Неопределено Тогда
		Результат = "";
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ДвоичныеДанные() Экспорт

	Возврат мДвоичныеДанныеТело;

КонецФункции