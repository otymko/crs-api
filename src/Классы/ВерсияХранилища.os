// Идентификатор версии хранилища (GUID)
Перем Идентификатор Экспорт;
// Порядковый номер версии хранилища
Перем Номер Экспорт;
// Дата версии хранилища
Перем Дата Экспорт;
// Внутренняя версия конфигурации
Перем ВнутренняяВерсия Экспорт;
// Версия конфигурации
Перем Версия Экспорт;
// Автор версии в хранилище
Перем Пользователь Экспорт;
// Номер версии платформы 1С
Перем Версия1С Экспорт;
// Комментарий к версии хранилища
Перем Комментарий Экспорт;
// Данные файла конфигурации
Перем Данные Экспорт;

Процедура ПриСозданииНаСервере()
	Пароль = Неопределено;
КонецПроцедуры

// Заполняет объект на основе входящих данных
//
// Параметры:
//   ВходящиеДанные - Соответствие - результат разбора XML с помощью библиотеки xmp-parser
//
Процедура Заполнить(ВходящиеДанные) Экспорт
	
	info = ВходящиеДанные["info"];

	Идентификатор    = Вспомогательный.ЗначениеСвойства(info, "versionID");
	Номер            = info["verNum"];
	Дата             = Вспомогательный.ВычислитьДату(Вспомогательный.ЗначениеСвойства(info, "date"));
	Пользователь     = Вспомогательный.ЗначениеСвойства(info, "userID");
	Версия           = Вспомогательный.ЗначениеСвойства(info, "code");
	ВнутренняяВерсия = СтрШаблон("%1.%2", info["cvermajor"], info["cverminor"]);
	Версия1С         = СтрШаблон("%1.%2.%3.%4",
	                             Вспомогательный.ЗначениеСвойства(info, "pvermajor"),
	                             Вспомогательный.ЗначениеСвойства(info, "pverminor"),
	                             Вспомогательный.ЗначениеСвойства(info, "pverbuild"),
	                             Вспомогательный.ЗначениеСвойства(info, "pverrevis"));
	Комментарий      = info["comment"];
	Данные           = info["file"];

КонецПроцедуры
